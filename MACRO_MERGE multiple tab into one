Sub CopyRangeFromMultiWorksheets()
    Dim sh As Worksheet
    Dim DestSh As Worksheet
    Dim Last As Long
    Dim CopyRng As Range

    With Application
        .ScreenUpdating = False
        .EnableEvents = False
    End With

    'Delete the sheet "RDBMergeSheet" if it exist
    Application.DisplayAlerts = False
    On Error Resume Next
    ActiveWorkbook.Worksheets("RDBMergeSheet").Delete
    On Error GoTo 0
   Application.DisplayAlerts = True

    'Add a worksheet with the name "RDBMergeSheet"
    Set DestSh = ActiveWorkbook.Worksheets.Add
    DestSh.Name = "RDBMergeSheet"

    'loop through all worksheets and copy the data to the DestSh
    For Each sh In ActiveWorkbook.Worksheets
        If sh.Name <> DestSh.Name Then

            'Find the last row with data on the DestSh
            Last = LastRow(DestSh)

            'Fill in the range that you want to copy
            Set CopyRng = sh.UsedRange

            'Test if there enough rows in the DestSh to copy all the data
            If Last + CopyRng.Rows.Count > DestSh.Rows.Count Then
                MsgBox "There are not enough rows in the Destsh"
                GoTo ExitTheSub
            End If
            
            'Remove filter from all worksheets, if you want to copy only filtered data
            'then remove the following line
            CopyRng.Parent.AutoFilterMode = False

            'This example copies values/formats, if you only want to copy the
            'values or want to copy everything look at the example below this macro
            CopyRng.Copy
            With DestSh.Cells(Last + 1, "A")
                .PasteSpecial xlPasteValues
                .PasteSpecial xlPasteFormats
                Application.CutCopyMode = False
            End With

            'Optional: This will copy the sheet name in the H column
            DestSh.Cells(Last + 1, "H").Resize(CopyRng.Rows.Count).Value = sh.Name

        End If
    Next



ExitTheSub:

    Application.GoTo DestSh.Cells(1)

    'AutoFit the column width in the DestSh sheet
    DestSh.Columns.AutoFit

    With Application
        .ScreenUpdating = True
        .EnableEvents = True
    End With
End Sub

Function LastRow(sh As Worksheet)
    On Error Resume Next
        LastRow = sh.Cells.Find(What:="*", _
                            After:=sh.Range("A1"), _
                            Lookat:=xlPart, _
                            LookIn:=xlFormulas, _
                            SearchOrder:=xlByRows, _
                            SearchDirection:=xlPrevious, _
                            MatchCase:=False).Row
    On Error GoTo 0
End Function



-----------------------------------------

Sub AddWorksheet()
Worksheets.Add().Name = "August"
Worksheets.Add().Name = "September"
Worksheets.Add().Name = "October"
Worksheets.Add().Name = "November"
Worksheets.Add().Name = "December"
Worksheets.Add().Name = "January"
End Sub






----------------------------------------

Sub MoveDataToSheetName()
'Determine how many rows from Sheet1 to copy
  lastListRow = Sheets(1).Range("A" & Rows.Count).End(xlUp).Row
'Loop through rows
    For myItems = 1 To lastListRow
'Set Sheet name based on value in Sheet1 Column A for each list item
     shtName = Sheets(1).Range("A" & myItems)
'Determine next empty row in Sheet with the same name
'as the value in Sheet1 Column A
     destRow = Sheets(shtName).Range("A" & Rows.Count).End(xlUp).Row + 1
'Copy current list item to proper sheet
      Rows(myItems).EntireRow.Copy _
        Destination:=Sheets(shtName).Range("A" & destRow)
'Loop
    Next
End Sub
